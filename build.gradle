plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'pw.aru.libs'
version = '1.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/natanbc/maven' }
}

dependencies {
    compile 'com.github.natanbc:reliqua:2.3.1'
    compile 'org.json:json:20170516'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'org.slf4j:slf4j-api:1.7.25'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'arudiscord'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/arudiscord/DD4J.git'
        publish = true
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn publishToMavenLocal
}
